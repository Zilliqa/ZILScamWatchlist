scilla_version 0

(***************************************************)
(*               Associated library                *)
(***************************************************)
import IntUtils
library FungibleToken

let one_msg = 
  fun (msg : Message) => 
  let nil_msg = Nil {Message} in
  Cons {Message} msg nil_msg

let two_msgs =
fun (msg1 : Message) =>
fun (msg2 : Message) =>
  let msgs_tmp = one_msg msg2 in
  Cons {Message} msg1 msgs_tmp
  
(* Error events *)
type Error =
| CodeIsSender
| CodeInsufficientFunds
| CodeInsufficientAllowance
| CodeIsNotManager

let make_error =
  fun (result : Error) =>
    let result_code = 
      match result with
      | CodeIsSender              => Int32 -1
      | CodeInsufficientFunds     => Int32 -2
      | CodeInsufficientAllowance => Int32 -3
      | CodeIsNotManager          => Int32 -4
      end
    in
    { _exception : "Error"; code : result_code }
  
let zero = Uint128 0

(* Dummy user-defined ADT *)
type Unit =
| Unit

let get_val =
  fun (some_val: Option Uint128) =>
  match some_val with
  | Some val => val
  | None => zero
  end

(***************************************************)
(*             The contract definition             *)
(***************************************************)

contract ScamToken
(
  contract_owner: ByStr20,
  name : String,
  symbol: String,
  decimals: Uint32,
  init_supply : Uint128,
  init_manager: ByStr20
)

(* Mutable fields *)

field total_supply : Uint128 = init_supply

field balances: Map ByStr20 Uint128 
  = let emp_map = Emp ByStr20 Uint128 in
    builtin put emp_map contract_owner init_supply

(*initial address that has access to all transitions*)
field manager: ByStr20 = init_manager

(**************************************)
(*             Procedures             *)
(**************************************)

procedure ThrowError(err : Error)
  e = make_error err;
  throw e
end

(* @dev:   check if transition is called by management contract.  *)
(* @param: send_address: Address that sent transition             *)
procedure IsManager(send_address: ByStr20)
  curr_manager <- manager;
  (*check if sender of transition is the management contract*)
  is_manager = builtin eq send_address curr_manager;
  match is_manager with
  | False =>
    err = CodeIsNotManager;
    ThrowError err
  | True =>
  end
end  

(* @dev:   mint tokens for recipient.                                         *)
(* @param: recipient: Address of the recipient whose balance is to increase.  *)
(* @param: amount:    Number of tokens to be minted.                          *)
procedure AuthorizedMint(recipient: ByStr20, amount: Uint128) 
  some_bal <- balances[recipient];
  bal = get_val some_bal;
  new_balance = builtin add amount bal;
  balances[recipient] := new_balance;
  current_total_supply <- total_supply;
  new_total_supply = builtin add current_total_supply amount;
  total_supply := new_total_supply;
  e = {_eventname: "Minted"; minter: _sender; recipient: recipient; amount: amount};
  event e
end

(* @dev:   Burn existing tokens. If from balance less than amount, error.          *)
(* @param: burn_account: Address of the token_owner whose balance is to decrease.  *)
(* @param: amount:       Number of tokens to be burned.                            *)
procedure AuthorizedBurnIfSufficientBalance(burn_account: ByStr20, amount: Uint128)
  o_get_bal <- balances[burn_account];
  bal = get_val o_get_bal;
  can_burn = uint128_le amount bal;
  match can_burn with
  | True =>
    (* Subtract amount from from *)
    new_balance = builtin sub bal amount;
    balances[burn_account] := new_balance;
    current_total_supply <- total_supply;
    new_total_supply = builtin sub current_total_supply amount;
    total_supply := new_total_supply;
    e = {_eventname: "Burnt"; burner: _sender; burn_account: burn_account; amount: amount};
    event e  
  | False =>
    err = CodeInsufficientFunds;
    ThrowError err
  end
end

(* @dev:   Moves an amount tokens from _sender to the recipient. Used by token_owner.  *)
(* @dev:   Balance of recipient will increase. Balance of _sender will decrease.       *)
(* @dev:   If balances less than amount, error                                         *)
(* @param: to:     Address of the recipient whose balance is increased.                *)
(* @param: amount: Amount of tokens to be sent.                                        *)
procedure AuthorizedMoveIfSufficientBalance(from: ByStr20, to: ByStr20, amount: Uint128)
  o_from_bal <- balances[from];
  bal = get_val o_from_bal;
  can_do = uint128_le amount bal;
  match can_do with
  | True =>
    (* Subtract amount from from and add it to to address *)
    new_from_bal = builtin sub bal amount;
    balances[from] := new_from_bal;
    (* Adds amount to to address *)
    get_to_bal <- balances[to];
    new_to_bal = match get_to_bal with
    | Some bal => builtin add bal amount
    | None => amount
    end;
    balances[to] := new_to_bal
  | False =>
    (* Balance not sufficient *)
    err = CodeInsufficientFunds;
    ThrowError err
  end
end

(***************************************)
(*             Transitions             *)
(***************************************)

(* @dev:   Change management contract. Only managers in management contract can call transitions.  *)
(* @param: new_manager: Address of new management contract.                                        *)
transition ChangeManager(new_manager: ByStr20)
  IsManager _sender;
  manager := new_manager;
  e = {_eventname: "ChangedManager"; new_manager: new_manager};
  event e  
end

(* @dev:   Mint new tokens. Only minter which is ssnlist can mint.            *)
(* @param: recipient: Address of the recipient whose balance is to increase.  *)
(* @param: amount:    Number of tokens to be minted.                          *)
transition Mint(recipient: ByStr20, amount: Uint128)
  IsManager _sender;
  AuthorizedMint recipient amount;
    (* Prevent sending to a contract address that does not support transfers of token *)
  msg_to_recipient = {_tag: "RecipientAcceptMint"; _recipient: recipient; _amount: zero; 
                      minter: _sender; recipient: recipient; amount: amount};
  msgs = one_msg msg_to_recipient;
  send msgs
end

(* @dev:   Burn existing tokens. Only contract_owner can burn.                     *)
(* @param: burn_account: Address of the token_owner whose balance is to decrease.  *)
(* @param: amount:       Number of tokens to be burned.                            *)
transition Burn(burn_account: ByStr20, amount: Uint128)
  IsManager _sender;
  AuthorizedBurnIfSufficientBalance burn_account amount;
  msg_to_sender = {_tag : "BurnSuccessCallBack"; _recipient : burn_account; _amount : zero; 
                    burner : _sender; burn_account : burn_account; amount : amount};
  msgs = one_msg msg_to_sender;
  send msgs
end

(* @dev:  Moves an amount tokens from _sender to the recipient. Used by token_owner. *)
(* @dev:  Balance of recipient will increase. Balance of _sender will decrease.      *)
(* @param: to:     Address of the recipient whose balance is increased.              *)
(* @param: amount: Amount of tokens to be sent.                                      *)
transition Transfer(from: ByStr20, to: ByStr20, amount: Uint128)
  IsManager _sender;
  AuthorizedMoveIfSufficientBalance from to amount;
  e = {_eventname : "TransferSuccess"; sender : from; recipient : to; amount : amount};
  event e;
  (* Prevent sending to a contract address that does not support transfers of token *)
  msg_to_recipient = {_tag : "RecipientAcceptTransfer"; _recipient : to; _amount : zero; 
                      sender : _sender; recipient : to; amount : amount};
  msgs = one_msg msg_to_recipient;
  send msgs
end